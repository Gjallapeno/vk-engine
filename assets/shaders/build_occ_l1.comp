#version 460
layout(local_size_x=8, local_size_y=8, local_size_z=8) in;

layout(binding=0) uniform usampler3D occSrc;
layout(r8ui, binding=1) uniform uimage3D occDst;

layout(push_constant) uniform PushConsts {
    int srcDim[3];
    int dstDim[3];
    int blockSize;
} pc;

void main(){
    ivec3 id = ivec3(gl_GlobalInvocationID);
    ivec3 dstDim = ivec3(pc.dstDim[0], pc.dstDim[1], pc.dstDim[2]);
    if(any(greaterThanEqual(id, dstDim))) return;
    ivec3 base = id * pc.blockSize;
    uint occ = 0u;
    for(int z=0; z<pc.blockSize && occ==0; ++z){
        for(int y=0; y<pc.blockSize && occ==0; ++y){
            for(int x=0; x<pc.blockSize; ++x){
                ivec3 p = base + ivec3(x,y,z);
                if(texelFetch(occSrc, p, 0).r > 0u){ occ = 1u; break; }
            }
        }
    }
    imageStore(occDst, id, uvec4(occ,0,0,0));
}
